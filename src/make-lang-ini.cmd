@echo off
REM This script generates the "lang.ini" and "lang-classical.ini"  files
REM for the installer to always include the latest translations

set LangList=""

for %%L in ( "%~dp0\gui\i18n\*.lng" ) do call :CreateLangString "%%~nL"

echo %LangList% >%TMP%\LangList.tmp
echo.
echo Please remove the leading and trailing double quotes ...
echo.
pause
notepad %TMP%\LangList.tmp

for %%F in ( "lang.ini" "lang-classical.ini" ) do call :CreateINI %%F >"%~dp0\installer\%%~F"

del /f /q %TMP%\LangList.tmp

goto :EOF

:CreateLangString
    if %LangList% == "" (
        set LangList="ListItems=%~1"
    ) else (
        set LangList="%LangList:"=%|%~1"
    )
goto :EOF

:CreateINI
    if %1 == "lang.ini" (
        set Field1Right=211
        set Field2Left=215
        set Field2Right=297
        set Field2Bottom=139
    ) else (
        set Field1Right=179
        set Field2Left=182
        set Field2Right=262
        set Field2Bottom=123
    )
    echo ; Ini file generated by the HM NIS Edit IO designer.
    echo [Settings]
    echo NumFields=3
    echo.
    echo [Field 1]
    echo Type=Label
    echo Text=Select a language pack to be used for UltraDefrag GUI, Configuration dialog and Scheduler.\r\n\r\nIf your native language is missing here or an existing translation is incomplete, feel free to submit your own translation. Your help will be much appreciated!
    echo Left=1
    echo Right=%Field1Right%
    echo Top=0
    echo Bottom=56
    echo.
    echo [Field 2]
    echo Type=Listbox
    echo Text=Listbox
    echo State=English (US)
    type %TMP%\LangList.tmp
    echo Left=%Field2Left%
    echo Right=%Field2Right%
    echo Top=0
    echo Bottom=%Field2Bottom%
    echo.
    echo [Field 3]
    echo Type=Link
    echo Text=Visit ultradefrag.wikispaces.com for more details...
    echo State=http://ultradefrag.wikispaces.com/
    echo Left=1
    echo Right=163
    echo Top=57
    echo Bottom=72
goto :EOF
